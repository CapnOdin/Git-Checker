;#Warn All, Off
#persistent
#SingleInstance Force

TraySetIcon("resources\github.ico")

#Include <Yaml>

guititle := "Git Checker"

threads := []
projects := Map()

MyGui := GuiCreate("+ReSize", guititle)
MyGui.OnEvent("Close", "GuiClose")
MyGui.OnEvent("Size", "GuiSize")

MyGui.MarginX := 5, MyGui.MarginY := 5
MyGui.SetFont("s11")

cEd := MyGui.Add("Edit", "Section w240")

cBtAdd := MyGui.Add("Button", "ys h25", "Add")
cBtAdd.OnEvent("Click", Func("AddPressed").Bind(cEd))

dist := cBtAdd.Pos.x - (cEd.Pos.x + cEd.Pos.w)

cTv := MyGui.Add("TreeView", "Section x5 xs w290 h280")
cTv.Move("w" cBtAdd.Pos.x - cTv.Pos.x + cBtAdd.Pos.w)
cTv.OnEvent("DoubleClick", (*) => DoubleClicked())
cTv.OnEvent("ContextMenu", (*) => RightClicked())

cBtCheck := MyGui.Add("Button", "x5 xs w290 h25", "Recheck")
cBtCheck.Move("w" cBtAdd.Pos.x - cBtCheck.Pos.x + cBtAdd.Pos.w)
cBtCheck.OnEvent("Click", (*) => CheckAll())

ImageListID := IL_Create(10)  ; Create an ImageList with initial capacity for 10 icons.
IL_Add(ImageListID, "resources\non.ico")
IL_Add(ImageListID, "resources\good.ico")
IL_Add(ImageListID, "resources\leastbad.ico")
IL_Add(ImageListID, "resources\lessbad.ico")
IL_Add(ImageListID, "resources\bad.ico")
IL_Add(ImageListID, "resources\line.ico")

cTv.SetImageList(ImageListID)

Populate()

MyGui.Show()

CheckAll()

;SetTimer(Func("CheckAll"), 60 * 1000)
return

#If WinActive("ahk_id " MyGui.hwnd)
	$Enter::
	{
		hwnd := MyGui.FocusedCtrl.Hwnd
		if(hwnd = edit.Hwnd) {
			AddPressed(cCBox)
		} else {
			Send("{Enter}")
		}
	}
#If

GitStatus(project, reset := False) {
	return RunWaitMany("git -C `"" project.path "`" status -s") != ""
}

GitCheckIfPushed(project) {
	return RunWaitMany("git -C `"" project.path "`" log --branches --not --remotes") != ""
}

GitCheckForUntracked(project) {
	return RunWaitMany("git -C `"" project.path "`" ls-files --others --exclude-standard") != ""
}

CheckStatus(project, counter := -1, reset := False) {
	Global cTv
	CoordMode("ToolTip", "Screen")
	ToolTip("Checking Status: " counter, 0, 0)
	cTv.Modify(project.tvId, "+Icon1")
	ToolTip("Checking changes: " counter, 0, 0)
	changes := GitStatus(project, reset)
	ToolTip("Checking commits: " counter, 0, 0)
	commits := GitCheckIfPushed(project)
	ToolTip("Checking untracked: " counter, 0, 0)
	untracked := GitCheckForUntracked(project)
	if(!changes && !commits && !untracked) {
		cTv.Modify(project.tvId, "+Icon2")
	} else if(!changes && !commits && untracked) {
		cTv.Modify(project.tvId, "+Icon3")
	} else if(!changes && commits) {
		cTv.Modify(project.tvId, "+Icon4")
	} else {
		cTv.Modify(project.tvId, "+Icon5")
	}
	ToolTip()
}

CheckAll() {
	Global projects
	static counter := 1
	reset := Mod(counter, 29) == 0
	for i, project in projects {
		CheckStatus(project, counter, reset)
		reset := False
	}
	counter++
}

AddProject(path, parent) {
	Global cTv, projects
	project := ProjectClass.new(path)
	name := project.name
	if(projects.Has(project.name)) {
		while(projects.Has(name := project.name " (" A_Index ")")) {
			
		}
	}
	projects[name] := project
	project.tvId := cTv.Add(project.name, parent, "Vis")
}

AddCategory(category, projects, patentID := 0) {
	Global cTv
	parent := cTv.Add(category, patentID, "Icon6")
	
	if(Type(projects) = "Array") {
		for i, path in projects {
			if(IsObject(path)) {
				AddCategory(i, path, parent)
			} else {
				AddProject(path, parent)
			}
		}
	} else if(Type(projects) = "Object") {
		for name, child in projects.OwnProps() {
			if(IsObject(child)) {
				AddCategory(name, child, parent)
			} else {
				AddProject(path, parent)
			}
		}
	}
}

Populate() {
	
	projects := Yaml(FileOpen("projects.json", "r`n").Read())[1]
	
	for category, paths in projects.OwnProps() {
		AddCategory(category, paths)
		
		/*
		parrent := cTv.Add(category)
		for i, path in paths {
			if(IsObject(path)) {
				
			}
			AddProject(path, parrent)
		}
		*/
	}
	
	;for i, path in StrSplit(FileOpen("projects.csv", "r`n").Read(), "`n") {
	;	AddProject(path)
	;}
}

AddPressed(edit, *) {
	path := edit.Text
	if(path) {
		ClearControl(edit)
		if(DirExist(path "\.git")) {
			;AddProject(path)
		}
	}
}

OpenTerminal(project) {
	Run("powershell -NoExit -Command `"& {cd \`"" project.path "\`"`n git status}`"")
}

DoubleClicked() {
	Global cTv, projects
	try {
		SelectedItemID := cTv.GetSelection()
		name := cTv.GetText(SelectedItemID)
		OpenTerminal(projects[name])
		CheckStatus(projects[name])
	}
}

RightClicked() {
	Global cTv, projects
	try {
		Click()
		Sleep(50)
		SelectedItemID := cTv.GetSelection()
		name := cTv.GetText(SelectedItemID)
		Run(projects[name].path)
	}
}

RunWaitMany(commands, timeout := 10000, reset := False) {
	static shell := ComObjCreate("WScript.Shell"), exec := shell.Exec(A_ComSpec " /q /k echo off"), _ := WinWaitHide(exec.ProcessID)
	; Open cmd.exe with echoing of commands disabled

	if(reset) {
		WinClose("ahk_pid " exec.ProcessID)
		shell := ComObjCreate("WScript.Shell")
		exec := shell.Exec(A_ComSpec " /q /k echo off")
		WinWaitHide(exec.ProcessID)
	}

	start := A_TickCount

	; Send the commands to execute, separated by newline
	exec.StdIn.WriteLine("cls`n" commands "`necho ")
	
	; Read and return the output of all commands
	output := ""
	while((char := exec.StdOut.Read(1)) != "") {
		if(A_TickCount - start > timeout) {
			Throw {"what": A_ThisFunc " timedout after " (A_TickCount - start) " while running (" commands ")", "file": A_LineFile, "line": A_LineNumber}
		}
		output .= char
	}
	output := StrReplace(StrReplace(output, ""), "`r")
	output := RegExReplace(output, "m)^$`n")
	if(SubStr(output, -1) = "`n") {
		output := SubStr(output, 1, -1)
	}
	return output
	;return exec.StdOut.ReadAll()
}

WinWaitHide(pID) {
	WinWait("ahk_pid " pID)
	WinHide("ahk_pid " pID)
}

ClearControl(control) {
	control.Text := ""
}

GuiSize(GuiObj, MinMax, Width, Height) {
	Global cBtAdd, cEd, cTv, cBtCheck, dist
	cBtAdd.Move("x" Width - cBtAdd.Pos.w - GuiObj.MarginX)
	cEd.Move("w" cBtAdd.Pos.x - cEd.Pos.x - dist)
	cBtCheck.Move("w" cBtAdd.Pos.x - cBtCheck.Pos.x + cBtAdd.Pos.w " y" Height - cBtCheck.Pos.h - GuiObj.MarginY)
	cTv.Move("w" cBtAdd.Pos.x - cTv.Pos.x + cBtAdd.Pos.w " h" cBtCheck.Pos.y - cTv.Pos.y - dist)
}

GuiClose(*) {
	ExitApp
}

Join(sep, params*) {
    for index,param in params
        str .= param . sep
    return SubStr(str, 1, -StrLen(sep))
}

Class ProjectClass {
	tvobj := ""

	__New(path) {
		this.path := path
		this.name := this.GetName()
	}
	
	GetName() {
		SplitPath(this.path, name)
		return name
	}
}
